plugins {
    alias(libs.plugins.android.application)
    id("com.chaquo.python")
}

import groovy.xml.XmlUtil

abstract class GenerateEnvTask extends DefaultTask {
    @InputFile
    abstract RegularFileProperty getEnvFile()

    @OutputFile
    abstract RegularFileProperty getOutputFile()

    @TaskAction
    void taskAction() {
        Properties props = new Properties()
        File inputFile = getEnvFile().get().getAsFile()

        if (inputFile.exists()) {
            inputFile.withInputStream { props.load(it) }
        }

        StringBuilder xmlContent = new StringBuilder()
        xmlContent.append('<?xml version="1.0" encoding="utf-8"?>\n')
        xmlContent.append('<manifest xmlns:android="http://schemas.android.com/apk/res/android">\n')
        xmlContent.append('    <application>\n')

        props.each { key, value ->
            String v = XmlUtil.escapeXml(value)
            xmlContent.append("        <meta-data android:name=\"env.${key}\" android:value=\"${v}\" />\n")
        }

        xmlContent.append('    </application>\n')
        xmlContent.append('</manifest>')

        getOutputFile().get().getAsFile().text = xmlContent.toString()
    }
}

android {
    namespace 'com.npdep.npps4'
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        applicationId "com.npdep.npps4"
        minSdk 24
        targetSdk 36
        versionCode 20260222
        versionName "0.0.20250222"

        ndk {
            abiFilters "arm64-v8a", "x86_64"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled  true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        aidl = true
    }
}

chaquopy {
    defaultConfig {
        version = "3.14"

        pyc {
            src = false
        }

        pip {
            options("--extra-index-url", "https://npps4-whl.npdep.com/")
            install("-r", "src/main/python/requirements.txt")
        }

        // All packages needs to be extracted.
        // Otherwise, alembic won't able to find any revisions.
        extractPackages("*")
    }
}

androidComponents {
    def metadataManifestTask = tasks.register("generateMetadataManifest", GenerateEnvTask) {
        envFile = rootProject.file("envvars.txt")
        outputFile = layout.buildDirectory.file("generated/metadata_manifest/AndroidManifest.xml")
    }

    onVariants(selector().all(), { variant ->
        variant.sources.manifests.addGeneratedManifestFile(
                metadataManifestTask,
                { it.getOutputFile() }
        )
    })
}

allprojects {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.viewpager2
    coreLibraryDesugaring libs.desugar.jdk.libs.nio
}